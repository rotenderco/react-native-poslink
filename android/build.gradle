buildscript {
  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:8.3.1'
  }
}

def reactNativeArchitectures() {
  def value = rootProject.getProperties().get("reactNativeArchitectures")
  return value ? value.split(",") : ["armeabi-v7a", "x86", "x86_64", "arm64-v8a"]
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: "com.android.library"

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def getExtOrDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties["Poslink_" + name]
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["Poslink_" + name]).toInteger()
}

static def supportsNamespace() {
  def parsed = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')
  def major = parsed[0].toInteger()
  def minor = parsed[1].toInteger()

  // Namespace support was added in 7.3.0
  return (major == 7 && minor >= 3) || major >= 8
}

android {
  if (supportsNamespace()) {
    namespace "com.poslink"

    sourceSets {
      main {
        manifest.srcFile "src/main/AndroidManifestNew.xml"
      }
    }
  }

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")

  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  sourceSets {
    main {
      jniLibs.srcDirs=["libs"]
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

repositories {
  mavenCentral()
  google()
  maven {
    // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
    url("$rootDir/../node_modules/react-native/android")
  }
}


dependencies {
  // For < 0.71, this will be from the local maven repo
  // For > 0.71, this will be replaced by `com.facebook.react:react-android:$version` by react gradle plugin
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-android:0.73.6"

//  compile(name: "PAX_POSLinkAndroid_20240425", ext: "aar")
//  compile fileTree(dir: "libs", include: ["*.aar"])
//  compileOnly files("./libs/PAX_POSLinkAndroid_20240425.aar")
  implementation fileTree(dir: "/libs", include: "*.jar")
  implementation "com.squareup.okhttp3:okhttp:4.12.0"
  implementation "com.squareup.okio:okio:3.9.0"
  implementation "com.google.code.gson:gson:2.10.1"
  implementation "com.jcraft:jsch:0.1.55"

  implementation files('libs/GLComm_V1.12.01_20230515.jar')
  implementation files('libs/PaxLog_1.0.11_20220921.jar')
  implementation files('libs/POSLink_Admin_Android_Plugin_V2.00.00_20230828.jar')
  implementation files('libs/POSLink_Core_Android_V2.00.03_20230828.jar')
  implementation files('libs/POSLink_Semi_Android_Plugin_V2.00.00_20230828.jar')
}

